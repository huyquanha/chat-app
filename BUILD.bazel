load("@gazelle//:def.bzl", "gazelle", "gazelle_binary")
load("@rules_go//go:def.bzl", "nogo")
load("@rules_go//proto:compiler.bzl", "go_proto_compiler")

# To find the label for the plugin: bazel query --output label '@com_connectrpc_connect//...'
go_proto_compiler(
    name = "connect_go_grpc_compiler",
    options = [
        "paths=source_relative",
        "package_suffix",
    ],
    plugin = "@com_connectrpc_connect//cmd/protoc-gen-connect-go:protoc-gen-connect-go",
    suffix = ".connect.go",
    visibility = ["//visibility:public"],
    deps = ["@com_connectrpc_connect//:connect"],
)

gazelle_binary(
    name = "gazelle_buf",
    languages = [
        "@gazelle//language/proto",  # Built-in rule from gazelle for Protos.
        # Any languages that depend on Gazelle's proto plugin must come after it.
        "@gazelle//language/go",
        # Loads the Buf extension, so buf_lint_test (and maybe buf_breaking_test)
        # targets can be generated by Gazelle.
        "@rules_buf//gazelle/buf",
    ],
)

# gazelle:exclude bazel-*
# gazelle:exclude web
# gazelle:go_proto_compilers @rules_go//proto:go_proto
## @rules_go//proto:go_proto needs to be repeated here. See: https://github.com/bazel-contrib/bazel-gazelle/issues/1445#issuecomment-1825014865
# gazelle:go_grpc_compilers @rules_go//proto:go_proto,//:connect_go_grpc_compiler
# gazelle:exclude protos/**/empty.go
# gazelle:resolve proto go buf/validate/validate.proto @build_buf_gen_go_bufbuild_protovalidate_protocolbuffers_go//buf/validate
gazelle(
    name = "gazelle",
    gazelle = ":gazelle_buf",
)

nogo(
    name = "my_nogo",
    vet = True,
    visibility = ["//visibility:public"],
    deps = [
        # Add the high-value ones that vet=True doesn't include
        "@org_golang_x_tools//go/analysis/passes/copylock:go_default_library",
        "@org_golang_x_tools//go/analysis/passes/lostcancel:go_default_library",
        "@org_golang_x_tools//go/analysis/passes/unreachable:go_default_library",
        "@org_golang_x_tools//go/analysis/passes/unmarshal:go_default_library",
        "@org_golang_x_tools//go/analysis/passes/structtag:go_default_library",
        "@org_golang_x_tools//go/analysis/passes/errorsas:go_default_library",
        "@org_golang_x_tools//go/analysis/passes/httpresponse:go_default_library",
    ],
)

exports_files(["buf.yaml"])
